;____________________________________________________________
;__                                                        __
;__      Внешняя функция                                   __
;__                                                        __
;__  Шифрование указанной области по ключу, находящемуся   __
;__  в другой области.                                     __
;__  (Разновидность метода гаммирования с обратной связью) __
;__                                                        __
;__  Входные  параметры :                                  __
;__                                                        __
;__  ES:DI - шифруемая область, CX - длина этой области;   __
;__  BP - коэффецент A, SI - коэффецент C в формуле для    __
;__  получения очередного ПСЧ гаммы шрифта:                __
;__    T(i+1) = (A*(T(i)+C) mod M.                         __
;__  DS:BX - адресуют эталонную область (ключ)             __
;__  AX - длина этой области                               __
;__                                                        __
;____________________________________________________________

        MODEL large
        .CODE
        public  _extra_crypto

_extra_crypto  proc   near
        cld                         ; Установить рпямое направление
                                    ;  цепочных пераций
        push      ax                ; Сохранить рабочие регистры
        push      bx
        push      dx
        push      si
        push      di
        push      cx                ; сохранить длину
                                    ;  шифруемой области
                                    ;  и коэффициент C
        push      si
        push      ax                ; сохранить длину
                                    ;  эталонной области
        mov       si,bx             ; установить адресацию
                                    ;  эталонной области
        xor       bx,bx             ; обнулить накопитель
                                    ;  контрольной суммы
        mov       cx,ax             ; получить длину эталонной
                                    ;  области в словах
        shr       cx,1
        jcxz      xc_2              ; эталонная область
                                    ;  занимает 1 байт
xc_1:   add       bx,cx             ; подсчитать контрольную
                                    ;  сумму эталонной
                                    ;  области памяти
        lodsw
        mul       bp
        add       bx,ax
        adc       bx,dx
        loop      xc_1
xc_2:   pop       ax                ; восстановить длину
                                    ;  эталонной области
        test      al,1              ; в эталонной области
                                    ;  целое число слов ?
        jz        xc_3              ; да, переход
        add       bl,byte ptr [si]  ; учесть последний или
                                    ;  единственный байт
                                    ;  эталонной области
xc_3:   pop       si                ; восстановить
                                    ;  коэфециент C и длину
                                    ;  шифруемой области
        pop       cx
        push      cx                ; сохранить длину
                                    ;  шифруемой области
        shr       cx,1              ; получить длину
                                    ;  шифруемой области
                                    ;  в словах
        jcxz      xc_5              ; шифруется один байт
xc_4:   mov       ax,bx             ; вычислить очуредной
                                    ;  коэфециент
                                    ;  случайного числа
        mul       bp
        add       ax,si
        mov       bx,ax             ; сохранить очуредной
                                    ;  коэффициент в регистре BX
        xor       word ptr es:[di],ax ; закодировать одно слово
                                    ;   области
        inc       di                ; перейти к следующему
                                    ;  слову области
        inc       di
        loop      xc_4
xc_5:   pop       cx                ; восстановить длину
                                    ;  шифруемой области
        test      cl,1              ; в шифруемой области
                                    ;  целое число слов ?
        jz        xc_6              ; да, переход
        xor       byte ptr es:[di],bl ; зашифровать последний
                                      ;  или единственный байт
                                      ;  шифруемой области
xc_6:   pop       di                ; восстановить рабочие
                                    ;  регистры
        pop       si
        pop       dx
        pop       bx
        pop       ax
        retf
_extra_crypto     endp

        end

